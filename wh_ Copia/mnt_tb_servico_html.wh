/*

Project      : gr5
Program      : mnt.tb.servico.html
Function     : mnt_tb_servico_html
Created on   : 19-Aug-2002   17:56:11
Descripition : Manutenção de valores de tabelas de serviços

*/

   private lcACTION     := '' ,;
           lcCD_TB_SERV := '' ,;
           lcWHERE      := '' ,;
           lcTABLE      := '' ,;
           lcTABLE_DET  := '' ,;
           lcITEM       := '' ,;
           lcVALIDA     := ''

   private laRESULT    := {} ,;
           laFIELDS    := {} ,;
           laSAVE      := {} ,;
           lASAVE_AUX  := {} ,;
           laVAR_WICDO := {} ,;
           laVAR_WRITE := {} ,;
           laAUX_WICDO := {} ,;
           laAUX       := {} ,;
           laPUT       := {} ,;
           laAPROVACAO := {} ,;
           laVALIDA    := {}

   private x     := 0 ,;
           liPOS := 0

   local laWHERE := {} as array

   lcACTION     := upper(WGet('ACTION','C'))
   lcCD_TB_SERV := WGet('CD_TB_SERVICO','C')
   lcTABLE      := 'tabela_servico'
   lcTABLE_DET  := 'vl_tabela_servico'
   laFIELDS     := structtable(WSet("DB_ACTIVE"),lcTABLE,1,'A')
   laLABELS     := structtable(WSet("DB_ACTIVE"),lcTABLE,11,'A')
   if lcCD_TB_SERV = 'New'
      lcCD_TB_SERV := '0'
   endif
   lcWHERE     := 'CD_TB_SERVICO='+lcCD_TB_SERV

   laAPROVACAO := {'Pendente',; //Gravar 0
                   'Faturar' ,; //Gravar 1
                   'Isentar'}   //Gravar 2

   laVAR_WICDO := {'VAR_CD_SERVICO',;
                   'VAR_NM_SERVICO',;
                   'VAR_VL_UNIT',;
                   'VAR_FL_APROVADO'}

   laVAR_WRITE := {'CD_TB_SERVICO',;
                   'CD_SERVICO',;
                   'VL_UNIT',;
                   'FL_APROVADO'}

   /* Campos obrigatórios
   * Formato do array abaixo
   * 1 - Campo para captura da informação no HTML - WGet()
   * 2 - Parte do label a exibir em caso de erro, está inf. e capturado do Wictrix
   */
   laVALIDA := {{'NM_TB_SERVICO',laLABELS[ascan(laFIELDS,'NM_TB_SERVICO')]},;
                {'CD_MOEDA',laLABELS[ascan(laFIELDS,'CD_MOEDA')]}}

   /* Relacionamentos que devem ser validados
   * Formato do array abaixo
   * 1 - Tabela
   * 2 - Campo para captura da informação no HTML - WGet()
   * 3 - Campo para montagem da cláusula WHERE usada em old_db_findrec()
   * 4 - Parte do label a exibir em caso de erro, está inf. e capturado do Wictrix
   */
   laFIND_REC := {{'moeda','CD_MOEDA','CD_MOEDA',laLABELS[ascan(laFIELDS,'CD_MOEDA')]}}

   if lcACTION='NEW' .or. lcACTION='ALTER'
      servicos()
   endif

   if lcACTION='NEW' .and. gbWAC_CREATE
     wPut('CD_TB_SERVICO','New')
     aadd(laPUT,laVAR_WICDO)
     for x := 2 to len(laRESULT)
         lcITEM := '_'+alltrim(str(x-1))
         aadd(laPUT,{laVAR_WICDO[1]+lcITEM,; //Código do Servico
                     laVAR_WICDO[2]+lcITEM,; //Descrição
                     laVAR_WICDO[3]+lcITEM,; //Valor
                     laVAR_WICDO[4]+lcITEM})  //Pré Aprovado [0] - Pendente, [1] - Faturar, [2] - Isentar

         wPut(laVAR_WICDO[1]+lcITEM,laRESULT[x,1])
         wPut(laVAR_WICDO[2]+lcITEM,laRESULT[x,2])
         wPut(laVAR_WICDO[3]+lcITEM,'0.00')

         // Inicialização de objetos Combo e Check ---------------------
         wPut(laVAR_WICDO[4]+lcITEM+'_OPT',laAPROVACAO) //Cbx Aprovação
     next x
     wPut('lstSERVICO',laPUT)
   elseif lcACTION='ALTER' .and. gbWAC_READ
     monta_grid()
   elseif lcACTION='SAVE' .and. gbWAC_WRITE
      // Validação de campos obrigatórios ------------------------------
      for x := 1 to len(laVALIDA)
          lcVALIDA := alltrim(WGet(laVALIDA[x,1],'C'))
          if len(lcVALIDA) = 0 .or. lcVALIDA == '0'
             walert(laVALIDA[x,2]+' é um campo obrigatório!')
             get2Put()
             servicos()
             monta_grid()
             return(.f.)
          endif
      next ii

      //Validação de chaves estrangeiras ------------------------------
      for ii := 1 to len(laFIND_REC)
          lcVALIDA := alltrim(WGet(laFIND_REC[ii,2],'C'))
          if !empty(lcVALIDA) .and. lcVALIDA <> '0'
             if len(db_findrec({laFIND_REC[ii,3]},laFIND_REC[ii,1],laFIND_REC[ii,3]+'='+lcVALIDA)) = 0
           walert(laFIND_REC[ii,4]+' é inválido(a) ou não pode ser encontrado(a)!')
           get2Put()
                servicos()
                monta_grid()
           return(.f.)
             endif
          endif
      next ii

      if lcCD_TB_SERV <> '0' .and. !empty(lcCD_TB_SERV)
         if db_delete(lcTABLE_DET,lcWHERE) == -1
            error_sys(db_error())
         endif
      endif

      aadd(laSAVE,laFIELDS)
      for x := 1 to len(laFIELDS)
          aadd(laSAVE_AUX,WGet(laFIELDS[x]))
      next x
      aadd(laSAVE,laSAVE_AUX)
      if db_replace(laSAVE,lcTABLE,{lcWHERE}) = -1
         error_sys(db_error())
      endif

      if alltrim(lcCD_TB_SERV) == '0'
         lcCD_TB_SERV := alltrim(str(db_insert_id()))
      endif

      laSAVE       := {}
      y            := 1
      lcITEM       := '_'+alltrim(str(y))
      lcFIELD_VAR  := 'VAR_'+laVAR_WRITE[2]+lcITEM  //Controle do laço - Cód do Servico
      aadd(laSAVE,laVAR_WRITE)
      do while WGet(lcFIELD_VAR,'B')
         lcAPROVACAO := upper(WGet('VAR_'+laVAR_WRITE[4]+lcITEM,'C')) //Flag de Aprovacao
         if lcAPROVACAO = 'PENDENTE'
            lcAPROVACAO := '0'
         elseif lcAPROVACAO = 'FATURAR'
            lcAPROVACAO := '1'
         else
            lcAPROVACAO := '2'
         endif
         if WGet('VAR_'+laVAR_WRITE[3]+lcITEM,'N') <> 0
            aadd(laSAVE,{val(lcCD_TB_SERV),;
                         WGet('VAR_'+laVAR_WRITE[2]+lcITEM,'N'),; //Cód do Servico
                         WGet('VAR_'+laVAR_WRITE[3]+lcITEM,'N'),; //Valor Unitário
                         lcAPROVACAO}) //Pré Aprovado [0] - Pendente, [1] - Faturar, [2] - Isentar
            aadd(laWHERE,'CD_TB_SERVICO='+lcCD_TB_SERV+' and CD_SERVICO='+WGet('VAR_'+laVAR_WRITE[2]+lcITEM,'C'))
         endif
         y++
         lcITEM      := '_'+alltrim(str(y))
         lcFIELD_VAR := 'VAR_'+laVAR_WRITE[2]+lcITEM  //Controle do laço - Cód do Servico
      enddo
      if len(laSAVE) > 1
         if db_replace(laSAVE,lcTABLE_DET,laWHERE) = -1
            error_sys(db_error())
         endif
      endif
      wSet('_USER_INTERFACE','lst.tb.servico.html')
      lst_tb_servico_html()
   endif
return

/*
* Function..: monta_grid()
* Objetivo..: Montar a lista de Servico  com seus respectivos valores, caso esteja cadastrados
* Parâmetros:
*    Nenhum
* Retorno...: Nenhum
*/
static function monta_grid()
   local laFLD_GRID := {} ,;
         laWHERE    := {} ,;
         laPUT      := {} as array
   local ii := 0 as int

   if lcACTION = 'ALTER'
      //lcQUERY := 'select '+lcFIELDS+',moeda.NM_MOEDA from '+lcTABLE+',moeda where '+lcWHERE+;
      //           ' and moeda.CD_MOEDA='+lcTABLE+'.CD_MOEDA'
      for ii := 1 to len(laFIELDS)
          aadd(laFLD_GRID,lcTABLE+'.'+laFIELDS[ii])
      next ii
      aadd(laFLD_GRID,'moeda.NM_MOEDA')
      laWHERE := {lcWHERE}
      if db_select(laFLD_GRID,lcTABLE,{{2,'moeda','moeda.CD_MOEDA=tabela_servico.CD_MOEDA'}},laWHERE) == -1
         error_sys('monta_grid: '+db_error())
      endif
      db2put(db_fetchall())

      //lcQUERY := 'select '+lcTABLE_DET+'.CD_SERVICO,';
      //                    +'servico.NM_SERVICO,';
      //                    +lcTABLE_DET+'.VL_UNIT,';
      //                    +lcTABLE_DET+'.FL_APROVADO';
      //                    +' from '+lcTABLE_DET+',servico where '+lcWHERE;
      //                    +' and '+lcTABLE_DET+'.CD_SERVICO=servico.CD_SERVICO order by '+lcTABLE_DET+'.CD_SERVICO'

      laFLD_GRID := {lcTABLE_DET+'.CD_SERVICO',;
                     'servico.NM_SERVICO',;
                     lcTABLE_DET+'.VL_UNIT',;
                     lcTABLE_DET+'.FL_APROVADO'}
      laWHERE    := {lcWHERE+' and '+lcTABLE_DET+'.CD_SERVICO=servico.CD_SERVICO'}
      if db_select(laFLD_GRID,lcTABLE_DET+',servico',,laWHERE,{lcTABLE_DET+'.CD_SERVICO'}) == -1
         error_sys('monta_grid: '+db_error())
      endif
      laAUX := db_fetchall()
   else
      laAUX := {}
   endif

   aadd(laAUX_WICDO,laVAR_WICDO)
   for x := 2 to len(laRESULT)
       lcITEM := alltrim(str(laRESULT[x,1]))  // Cód. Servico
       liPOS  := ascan(laAUX, {|laFIND| alltrim(str(laFIND[1])) == lcITEM})

       /* Se econtrar o Servico no lançamento, junta com a matriz primaria */
       if liPOS > 0
          aadd(laAUX_WICDO,laAUX[liPOS])
       else
          aadd(laAUX_WICDO,laRESULT[x]) //Adiciona Código do Servico e Descrição de uma vez!
          aadd(laAUX_WICDO[x],'0.00')   //Valor unitário
          aadd(laAUX_WICDO[x],'')       //Flag de aprovação (FL_APROVADO)
       endif
   next x

   laRESULT := {}
   aadd(laRESULT,laVAR_WICDO)
   for x := 2 to len(laAUX_WICDO)
       lcITEM := '_'+alltrim(str(x-1))
       aadd(laRESULT,{laVAR_WICDO[1]+lcITEM,; //Código do Servico
                      laVAR_WICDO[2]+lcITEM,; //Descrição
                      laVAR_WICDO[3]+lcITEM,; //Valor
                      laVAR_WICDO[4]+lcITEM}) //Pré Aprovado [0] - Pendente, [1] - Faturar, [2] - Isentar
       for y := 1 to len(laVAR_WICDO)
           if laVAR_WICDO[y] = 'VAR_FL_APROVADO'
              wPut(laVAR_WICDO[y]+lcITEM+'_OPT',laAPROVACAO)
              wPut(laVAR_WICDO[y]+lcITEM,laAPROVACAO[val(laAUX_WICDO[x,ascan(laVAR_WICDO,'VAR_FL_APROVADO')])+1])
              loop
           endif
           wPut(laVAR_WICDO[y]+lcITEM,laAUX_WICDO[x,y])
       next y
   next x
   wPut('lstSERVICO',laRESULT)
return(nil)

/*
* Function..: servicos()
* Objetivo..: Selecionar servico
* Parâmetros:
*    Nenhum
* Retorno...: Nenhum
*/
static function servicos()
   if db_select({'CD_SERVICO','NM_SERVICO'},'servico',,,{'CD_SERVICO'}) == -1
      error_sys('servicos: '+db_error())
   endif
   laRESULT := db_fetchall()
   if len(laRESULT) == 0
      wSet('_USER_INTERFACE','lst.tb.servico.html')
      lst_tb_servico_html()
   endif
return(nil)


