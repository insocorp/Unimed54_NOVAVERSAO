/*

Project      : gr5
Program      : cfg.pst.juros.html
Function     : cfg_pst_juros_html
Created on   : 09-May-2006   10:31:38
Descripition : Configuração de Juros cobrados nos processos.

*/

   local lcACTION      := '' ,;
         lcWHERE       := '' ,;
         lcTABLE       := '' ,;
         lcTP_PASTA    := '' ,;
         lcNR_CONTROLE := '' ,;
         lcVALIDA      := '' ,;
         lcAM_INICIO   := '' ,;
         lcAM_FIM      := '' as string

   local laRESULT   := {} ,;
         laFIELDS   := {} ,;
         laSAVE     := {} ,;
         laSAVE_AUX := {} ,;
         laVALIDA   := {} as array

   local ii    := 0 ,;
         lnPOS := 0 as int

   local llERROR := .f. as logical

   lcACTION       := upper(WGet('ACTION','C'))
   lcTP_PASTA     := WGet('TP_PASTA','C')
   lcNR_CONTROLE  := WGet('NR_CONTROLE','C')
   if val(lcNR_CONTROLE) == 0
       lcNR_CONTROLE := '0'
   endif
   lcTABLE        := 'pasta_config_juros'
   lcWHERE        := 'TP_PASTA = '+DLAP+lcTP_PASTA+DLAP+' and NR_CONTROLE = '+lcNR_CONTROLE
   laFIELDS       := structtable(WSet("DB_ACTIVE"),lcTABLE,1,'A')

   /* Campos obrigatórios
   * Formato do array abaixo
   * 1 - Campo para captura da informação no HTML - WGet()
   * 2 - Parte do label a exibir em caso de erro, está inf. e capturado do Wictrix
   */
   laVALIDA := {'AM_INICIO'}
   init_reqfield(WSet('_USER_INTERFACE'),laVALIDA)

   /* Combo*/
   WPut('FL_JUROS.option',{'Simples','Composto'})
   WPut('FL_JUROS.optionvalue',{'S','C'})
   WPut('TP_PASTA',lcTP_PASTA)
   WPut('TITLE','Juros')

   if lcACTION == 'ALTER' .and. gbWAC_READ
      if db_select(laFIELDS,lcTABLE,,{lcWHERE}) == -1
         error_sys(db_error())
      endif
      laRESULT := db_fetchall()
      if len(laRESULT) > 0
         db2Put(laRESULT)
      endif
   elseif lcACTION == 'SAVE' .and. gbWAC_WRITE
       if verif_reqfield(WSet('_USER_INTERFACE'),laVALIDA)
          lcAM_INICIO := WGet('AM_INICIO','C')
          lcAM_FIM    := WGet('AM_FIM','C')
          if empty(lcAM_FIM)
             lcAM_FIM := '999999'
          endif

          if (lcAM_INICIO > lcAM_FIM) .and. ! empty(lcAM_FIM)
             walert('Data inicial não pode ser maior que final')
             get2put()
          else

             //Validação de periodo
             if db_select({'AM_INICIO','AM_FIM'},;
                          'pasta_config_juros',,;
                          {'TP_PASTA='+DLAP+lcTP_PASTA+DLAP,'NR_CONTROLE <> '+lcNR_CONTROLE},;
                          {'AM_INICIO'}) == -1
                error_sys(db_error())
             endif
             laRESULT := db_fetchall()
             llERRO := .f.
             for ii := 2 to len(laRESULT)
                lcAUX  := laRESULT[ii,1]
                lcAUX2 := laRESULT[ii,2]
                if empty(lcAUX2)
                   lcAUX2 := '999999'
                endif

                //Verifica se na Data inicio existe algum periodo iniciando ou terminando
                if lcAM_INICIO >= lcAUX .and. lcAM_INICIO <= lcAUX2
                   walert('Juro ja definido para período!\nVerifique a Data Inicial.')
                   get2put()
                   llERRO := .t.
                   exit
                endif
                //Verifica se na Data fim existe algum periodo iniciando ou terminando
                if lcAM_FIM >= lcAUX .and. lcAM_FIM <= lcAUX2
                   walert('Juro ja definido para período!\nVerifique a Data Final.')
                   get2put()
                   llERRO := .t.
                   exit
                endif
             next ii
             if ! llERRO
                aadd(laSAVE,laFIELDS)
                for ii := 1 to len(laFIELDS)
                   aadd(laSAVE_AUX,WGet(laFIELDS[ii]))
                next ii
                aadd(laSAVE,laSAVE_AUX)
                if db_replace(laSAVE,lcTABLE,{lcWHERE}) = -1
                   error_sys(db_error())
                endif
             endif
          endif
       endif
   elseif lcACTION == 'DELETE' .and. gbWAC_DELETE
       if db_delete(lcTABLE,lcWHERE) = -1
          error_sys(db_error())
       endif
   endif

   if db_select({'pasta_config_juros.NR_CONTROLE',;
                 'pasta_config_juros.AM_INICIO',;
                 'pasta_config_juros.AM_FIM',;
                 'pasta_config_juros.PC_JUROS',;
                 'pasta_config_juros.FL_JUROS'},;
                 lcTABLE,,;
                {'pasta_config_juros.TP_PASTA='+DLAP+lcTP_PASTA+DLAP},;
                {'pasta_config_juros.AM_INICIO'}) == -1
      error_sys(db_error())
   endif
   laRESULT := db_fetchall()
   if len(laRESULT) > 0
      aadd(laRESULT[1],'(row)')
      for ii := 2 to len(laRESULT)
         aadd(laRESULT[ii],'(row)wicaction/cfg.pst.juros.html?ACTION=ALTER'+;
                        '&TP_PASTA='+lcTP_PASTA+;
                        '&NR_CONTROLE='+str(laRESULT[ii,db_fetchncol(laRESULT,'NR_CONTROLE')]))
         lnPOS := db_fetchncol(laRESULT,'FL_JUROS')
         if laRESULT[ii,lnPOS] == 'C'
            laRESULT[ii,lnPOS] := 'Composto'
         elseif laRESULT[ii,lnPOS] == 'S'
            laRESULT[ii,lnPOS] := 'Simples'
         endif
      next ii

      /* Remove coluna NR_CONTROLE da Grid */
      lnPOS := ascan(laRESULT[1],'NR_CONTROLE')
      if lnPOS > 0
         adel(laRESULT[1],lnPOS)
         asize(laRESULT[1],len(laRESULT[1])-1)
         for ii := 2 to len(laRESULT)
            adel(laRESULT[ii],lnPOS)
            asize(laRESULT[ii],len(laRESULT[ii])-1)
         next ii
      endif
      WPut('GRID_PST_JUROS',grid_create(laRESULT))
   endif
return
