/*

Project      : gr5
Program      : psab.sindicancia.html
Function     : psab_sindicancia_html
Created on   : 28-Mar-2006   10:28:26
Descripition : Aba Sindicâncias de Pasta

*/

   local lcACTION           := '' ,;
         lcWHERE            := '' ,;
         lcWHERE1           := '' ,;
         lcWHERE2           := '' ,;
         lcTABLE            := '' ,;
         lcTP_PASTA         := '' ,;
         lcNR_PASTA         := '' ,;
         lcNR_SINDICANCIA   := '' ,;
         lcCIP_DISTRIB_PARA := '' as string
   local laRESULT      := {} ,;
         laSAVE        := {} ,;
         laSAVE_AUX    := {} ,;
         laFIELDS      := {} ,;
         laLABELS      := {} ,;
         laVALIDA      := {} as array
   local ii := 0 as int

   lcACTION           := upper(alltrim(WGet('ACTION','C')))
   lcTP_PASTA         := WGet('TP_PASTA','C')
   wac_tp_pasta(lcTP_PASTA) // Função para verificação de permissões por pasta, encontra-se na func.wic
   lcNR_PASTA         := alltrim(WGet('NR_PASTA','C'))
   lcNR_SINDICANCIA   := alltrim(WGet('NR_SINDICANCIA','C'))
   lcCIP_DISTRIB_PARA := WGet('CIP_DISTRIB_PARA','C')
   lcTABLE            := 'pasta_contrato_sindicancia'
   lcWHERE            := 'pasta_contrato_sindicancia.NR_PASTA = '+lcNR_PASTA+' and pasta_contrato_sindicancia.NR_SINDICANCIA ='+DLAP+lcNR_SINDICANCIA+DLAP
   lcWHERE1           := 'NR_PASTA = '+lcNR_PASTA+' and NR_SINDICANCIA ='+DLAP+lcNR_SINDICANCIA+DLAP
   laFIELDS           := structtable(WSet("DB_ACTIVE"),lcTABLE,1,'N')
   laLABELS           := structtable(WSet("DB_ACTIVE"),lcTABLE,11,'N')
   laVALIDA           := {'NR_SINDICANCIA'}
   init_reqfield(WSet('_USER_INTERFACE'),laVALIDA)
   // Glauber 06/01/2011
   AbasPerfil(lcTP_PASTA,laFIELDS)

   if empty(lcNR_PASTA)
      error_sys('Pasta inexistente')
      return(.f.)
   endif
   make_psthead(lcNR_PASTA,lcTP_PASTA) //Cabeçalho

   if lcACTION = 'ALTER' .and. gbWAC_READ
      alter(laFIELDS,lcTABLE,lcNR_PASTA,lcNR_SINDICANCIA)

   elseif lcACTION = 'SAVE' .and. gbWAC_WRITE
      if verif_reqfield(WSet('_USER_INTERFACE'),laVALIDA)
         aadd(laSAVE,laFIELDS)
         for ii := 1 to len(laFIELDS)
            aadd(laSAVE_AUX,WGet(laFIELDS[ii]))
         next ii
         aadd(laSAVE,laSAVE_AUX)

         psab_logs(laSAVE,lcTABLE,lcWHERE,lcNR_PASTA,'Sindicância')

         if db_replace(laSAVE,lcTABLE,{lcWHERE}) = -1
            error_sys(db_error())
         endif

         if ! empty(lcCIP_DISTRIB_PARA)
            laSAVE := {{'NR_PASTA',;
                        'NR_SINDICANCIA',;
                        'CIP_DISTRIB_PARA',;
                        'DT_DISTRIBUICAO',;
                        'SINDIST_EXTRA'},;
                       {WGet('NR_PASTA'),;
                        WGet('NR_SINDICANCIA'),;
                        WGet('CIP_DISTRIB_PARA'),;
                        WGet('DT_DISTRIBUICAO'),;
                        WGet('SINDIST_EXTRA')}}

            lcWHERE2 := 'NR_PASTA='+lcNR_PASTA+' and '
            lcWHERE2 += 'NR_SINDICANCIA='+DLAP+lcNR_SINDICANCIA+DLAP+' and '
            lcWHERE2 += 'CIP_DISTRIB_PARA='+DLAP+lcCIP_DISTRIB_PARA+DLAP+' and '
            lcWHERE2 += 'DT_DISTRIBUICAO='+DLAP+WGet('DT_DISTRIBUICAO','DS')+DLAP

            if db_replace(laSAVE,'pasta_contrato_sindicancia_dist',{lcWHERE2}) == -1
               error_sys(db_error())
            endif
            psab_logs(laSAVE,'pasta_contrato_sindicancia_dist',lcWHERE2,lcNR_PASTA,'Sindicância')

            alter(laFIELDS,lcTABLE,lcNR_PASTA,lcNR_SINDICANCIA)
         endif
      endif

   elseif lcACTION = 'DELETE' .and. gbWAC_DELETE
      lcWHERE1           := 'NR_PASTA = '+DLAP+lcNR_PASTA+DLAP+' and NR_SINDICANCIA ='+DLAP+lcNR_SINDICANCIA+DLAP
      if !empty(lcNR_PASTA) .and. !empty(lcNR_SINDICANCIA)
         psab_logdel(lcNR_PASTA,'NR_SINDICANCIA',lcNR_SINDICANCIA,,'Sindicância')
         if db_select({'NR_PASTA','NR_SINDICANCIA'},'pasta_contrato_sindicancia_dist',,{lcWHERE1}) == -1
            error_sys(db_error())
         endif
         laRESULT := db_fetchall()
         if len(laRESULT) > 1
            if db_delete('pasta_contrato_sindicancia_dist',lcWHERE1) == -1
               error_sys(db_error())
            endif   
         endif
           
         if db_delete(lcTABLE,lcWHERE) == -1
            error_sys(db_error())
         endif
      endif
   elseif lcACTION = 'DELETE_DISTRIB' .and. gbWAC_DELETE
      if !empty(lcNR_PASTA) .and. !empty(lcNR_SINDICANCIA)
         psab_logdel(lcNR_PASTA,'CIP_DISTRIB_PARA',lcCIP_DISTRIB_PARA,,'Sindicância')
         lcWHERE2 := 'NR_PASTA='+lcNR_PASTA+' and '
         lcWHERE2 += 'NR_SINDICANCIA='+DLAP+lcNR_SINDICANCIA+DLAP+' and '
         lcWHERE2 += 'CIP_DISTRIB_PARA='+DLAP+lcCIP_DISTRIB_PARA+DLAP+' and '
         lcWHERE2 += 'DT_DISTRIBUICAO='+DLAP+WGet('DT_DISTRIBUICAO','DS')+DLAP

         if db_delete('pasta_contrato_sindicancia_dist',lcWHERE2) == -1
            error_sys(db_error())
         endif

         alter(laFIELDS,lcTABLE,lcNR_PASTA,lcNR_SINDICANCIA)
      endif
   endif
   if db_select({'pasta_contrato_sindicancia.NR_SINDICANCIA',;
                 'pasta_contrato_sindicancia.NR_PROTOCOLO',;
                 'pasta_contrato_sindicancia.NR_PROCESSO_ST',;
                 'pasta_contrato_sindicancia.DT_PROCESSO_ST',;
                 'pasta_contrato_sindicancia.NR_PROCESSO_MP',;
                 'pasta_contrato_sindicancia.DT_PROCESSO_MP',;
                 'pasta_contrato_sindicancia.NR_PROCESSO_PGE',;
                 'pasta_contrato_sindicancia.DT_PROCESSO_PGE'},;
                'pasta_contrato_sindicancia',,;
                {'pasta_contrato_sindicancia.NR_PASTA='+lcNR_PASTA},;
                {'pasta_contrato_sindicancia.NR_SINDICANCIA'}) == -1
      error_sys(db_error())
   endif
   laRESULT := db_fetchall()
   if len(laRESULT) > 1
      aadd(laRESULT[1],'(row)')
      for ii := 2 to len(laRESULT)
         aadd(laRESULT[ii],'(row)'+wic_action('wicaction/psab.sindicancia.html?'+;
                                              'TP_PASTA='+lcTP_PASTA+;
                                              '&NR_PASTA='+lcNR_PASTA+;
                                              '&NR_SINDICANCIA='+laRESULT[ii, db_fetchncol(laRESULT,'NR_SINDICANCIA')]+;
                                              '&ACTION=ALTER'))
      next ii
      WPut('GRID_SINDICANCIA',grid_create(laRESULT))
   endif
   pasta_abas(lcTP_PASTA)
return

static function alter(faFIELDS,fcTABLE,fcNR_PASTA,fcNR_SINDICANCIA)
   if db_select(faFIELDS,fcTABLE,,{'NR_PASTA='+fcNR_PASTA+' and NR_SINDICANCIA='+DLAP+fcNR_SINDICANCIA+DLAP}) == -1
      error_sys(db_error())
   endif
   db2put(db_fetchall())

   if db_select({'pasta_contrato_sindicancia_dist.CIP_DISTRIB_PARA',;
                 'pfpj.PFPJ',;
                 'pasta_contrato_sindicancia_dist.DT_DISTRIBUICAO',;
                 'pasta_contrato_sindicancia_dist.SINDIST_EXTRA'},;
                 'pasta_contrato_sindicancia_dist',;
                 {{2,'pfpj','pfpj.CIP=pasta_contrato_sindicancia_dist.CIP_DISTRIB_PARA'}},;
                 {'pasta_contrato_sindicancia_dist.NR_PASTA='+fcNR_PASTA+;
                  ' and pasta_contrato_sindicancia_dist.NR_SINDICANCIA='+DLAP+fcNR_SINDICANCIA+DLAP},;
                 {'pasta_contrato_sindicancia_dist.DT_DISTRIBUICAO','pfpj.PFPJ'}) == -1
      error_sys(db_error())
   endif
   WPut('lstDISTRIB_PARA',db_fetchall())
return (nil)







