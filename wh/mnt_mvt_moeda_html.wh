/*

Project      : gr5
Program      : mnt.mvt.moeda.html
Function     : mnt_mvt_moeda_html
Created on   : 15-Aug-2002   14:24:03
Descripition : Manutenção de Variações de Moeda

*/

   local lcACTION       := '',;
         lcWHERE        := '',;
         lcTABLE        := '',;
         lcCD_MOEDA     := '',;
         lcDT_REF       := '',;
         lcVALIDA       := '' as string

   local laRESULT   := {} ,;
         laSAVE     := {} ,;
         laSAVE_AUX := {} ,;
         laFIELDS   := {} ,;
         laVALIDA   := {} ,;
         laORDER_BY := {} as array

   local ii := 0 as int

   lcACTION       := upper(WGet('ACTION','C'))
   lcCD_MOEDA     := WGet('CD_MOEDA','C')
   lcDT_REF       := WGet('DT_REF','DS')
   lcTABLE        := 'mvt_moeda'
   lcWHERE        := 'CD_MOEDA = '+lcCD_MOEDA+' and DT_REF = '+DLAP+lcDT_REF+DLAP
   laFIELDS       := structtable(WSet("DB_ACTIVE"),lcTABLE,1,'N')
   laLABELS       := structtable(WSet("DB_ACTIVE"),lcTABLE,11,'N')
   laFIELDS_MOEDA := structtable(WSet("DB_ACTIVE"),'moeda',1,'N')
   laORDER_BY     := {'DT_REF'}

   /* Campos obrigatórios
   * Formato do array abaixo
   * 1 - Campo para captura da informação no HTML - WGet()
   * 2 - Parte do label a exibir em caso de erro, esta inf. á capturada do Wictrix
   */
   laVALIDA   := {{'DT_REF',laLABELS[ascan(laFIELDS,'DT_REF')]}}
   for ii:= 1 to len(laVALIDA)
      *WPut(laVALIDA[ii,1]+'.label.style.color','#FF0000')
      *WPut(laVALIDA[ii,1]+'.label.style.font-weight','bold')
      WPut(laVALIDA[ii,1]+'.style.border','2px solid red')
   next ii


   if empty(lcCD_MOEDA)
      wSet('_USER_INTERFACE','lst.mvt.moeda.html')
      lst_mvt_moeda_html()
      return
   elseif empty(lcACTION)
      lcACTION = 'NEW'
   endif

   if db_select(laFIELDS_MOEDA,'moeda',,{'CD_MOEDA='+lcCD_MOEDA}) == -1
      error_sys(db_error())
   endif
   laRESULT := db_fetchall()
   if len(laRESULT) > 1
      db2put(laRESULT)
   else
      wSet('_USER_INTERFACE','lst.mvt.moeda.html')
      lst_mvt_moeda_html()
   endif
   laRESULT := {}
   if lcACTION = 'NEW' .and. gbWAC_CREATE
      wPut('TITLE','Inclusão de nova Variação de moeda')
   elseif lcACTION = 'ALTER' .and. gbWAC_READ
      if db_select(laFIELDS,lcTABLE,,{lcWHERE}) == -1
         error_sys(db_error())
      endif
      db2put(db_fetchall())
      wPut('TITLE','Alteração da Variação de moeda '+ str(getWPut('DT_REF')))
   elseif lcACTION = 'SAVE' .and. gbWAC_WRITE
      for ii := 1 to len(laVALIDA)
          if valtype(WGet(laVALIDA[ii,1])) == 'D'
             lcVALIDA := alltrim(strtran(WGet(laVALIDA[ii,1],'C'),'/'))
          else
             lcVALIDA := alltrim(WGet(laVALIDA[ii,1],'C'))
          endif

          if len(lcVALIDA) == 0
             walert(laVALIDA[ii,2]+' é um campo obrigatório!')
             get2Put()
             lista_moedas(lcTABLE,{'DT_REF','VL_MOEDA'},lcCD_MOEDA,laORDER_BY)
             return(.f.)
          endif
      next ii
      aadd(laSAVE,laFIELDS)
      for ii := 1 to len(laFIELDS)
         aadd(laSAVE_AUX,WGet(laFIELDS[ii]))
      next ii
      aadd(laSAVE,laSAVE_AUX)
      if db_replace(laSAVE,lcTABLE,{lcWHERE}) = -1
         error_sys(db_error())
      endif
      wPut('TITLE','Inclusão de nova Variação de moeda')
   elseif lcACTION = 'DELETE' .and. gbWAC_DELETE
      if db_delete(lcTABLE,lcWHERE) == -1
         error_sys(db_error())
      endif
   endif
   lista_moedas(lcTABLE,{'DT_REF','VL_MOEDA'},lcCD_MOEDA,laORDER_BY)
return

/*
* Function..: lista_moedas(fcTABLE,faFIELDS,fcMOEDA,faORDER_BY)
* Objetivo..: Validar uma lista de usuários
* Parâmetros:
*    fcTABLE...: Tabela alvo
*    faFIELDS..: Lista de campos a selecionar
*    fcMOEDA...: Código Moeda
*    faORDER_BY: Ordenação
* Retorno...: nil
*/
static function lista_moedas(fcTABLE,faFIELDS,fcMOEDA,faORDER_BY)
   local ii := 0 as int
   local laRES := {} as array
   if db_select(faFIELDS,fcTABLE,,{'CD_MOEDA='+fcMOEDA},faORDER_by) == -1
      error_sys('lista_moedas: '+db_error())
   endif
   laRES := db_fetchall()
   if len(laRES) > 1
      aadd(laRES[1],'(row)')
      for ii := 2 to len(laRES)
         aadd(laRES[ii],'(row)'+wic_action('wicaction/mnt.mvt.moeda.html'+;
                                              '?CD_MOEDA='+fcMOEDA+;
                                              '&DT_REF='+dtoc(laRES[ii, db_fetchncol(laRES,'DT_REF')])+;
                                              '&ACTION=ALTER'))
      next ii
      wPut('GRID_MOEDA',grid_create(laRES))
   endif
return(nil)







