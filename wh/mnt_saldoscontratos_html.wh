/*

Project      : isj_45
Program      : mnt.saldoscontratos.html
Function     : mnt_saldoscontratos_html
Created on   : 18-Dec-2014   15:28:14
Descripition :

*/


    local lcTable         := 'saldocontratos' as String
    local laFIELDS        := {'TP_PASTA',;
                              'ID_PLATAFORMA',;
                              'CD_CCUSTO',;
                              'CD_JURCC'} as Array

    local laSAVE          := {},;
          laPUT           := {},;
          laSAVE_AUX      := {} as Array

    local lcAction        := Upper(WGet('ACTION')) as String
    local lcWhere         := '' as String
    local llErro          := .F.
    local ii              := 0 as int


    WPut('TITLE','Saldos dos Contratos')

    // Alimentar o combo de tipo de pasta
    laPUT := loadcboxorder_build('pasta_config','TP_PASTA',.t.,{'TP_PASTA'})
    Wput('TP_PASTA_OPT',laPUT)

    lcWhere := 'TP_PASTA = ' + DLAP + Wget('TP_PASTA') + DLAP + ' and ID_PLATAFORMA = ' + Wget('DESC_PLATAFORMA','C')

    if lcACTION == 'ALTER' .and. gbWAC_READ
       
       NovoCbox('DESC_PLATAFORMA','ID_PLATAFORMA','DESC_PLATAFORMA','plataforma','TP_PASTA='+DLAP +Wget('TP_PASTA')+DLAP,'DESC_PLATAFORMA')

       aadd(laFIELDS,'NM_CCUSTO')
       aadd(laFIELDS,'SALDO')
       aadd(laFIELDS,'CD_JURCC')
       aadd(laFIELDS,'NM_JURCC')

       for ii := 1 to len(laFIELDS)
           if(laFIELDS[ii] == 'ID_PLATAFORMA')
              WPut('DESC_PLATAFORMA',WGet(laFIELDS[ii]))
           else
              WPut(laFIELDS[ii],WGet(laFIELDS[ii]))
           endif
       next ii

    elseif lcACTION == 'SAVE' .and. gbWAC_WRITE

           if (Empty(Wget('TP_PASTA')))
               WAlert('O campo '+lblfscreen('TP_PASTA') + ' é de preenchimento obrigatório')
               llErro := .T.
           endif
    
           if Wget('DESC_PLATAFORMA','N') == 0
               WAlert('O campo '+lblfscreen('DESC_PLATAFORMA') + ' é de preenchimento obrigatório')
               llErro := .T.
           endif

           if ! llErro
              aadd(laSAVE,laFIELDS)
              for ii := 1 to len(laFIELDS)
                  if(laFIELDS[ii] == 'ID_PLATAFORMA')
                     aadd(laSAVE_AUX,WGet('DESC_PLATAFORMA'))
                  else
                     aadd(laSAVE_AUX,WGet(laFIELDS[ii]))
                  endif
              next ii
              aadd(laSAVE,laSAVE_AUX)

              if db_replace(laSAVE,lcTable,{lcWhere}) == -1
                 error_sys(db_error())
              endif
           endif
    elseif lcACTION == 'DELETE' .and. gbWAC_DELETE
           if Wget('SALDO','N') > 0
               WAlert('Não é possível excluir saldos com movimentação !')
               llErro := .T.
           endif
           if ! llErro
                if db_delete(lcTable,lcWhere) == -1
                   error_sys(db_error())
                endif
           endif

    endif


    GridPaginada()


return



/*
* Glauber - 12/2014
*
* Function..: GridPaginada()
* Objetivo..: Monta a grid paginada com os dados.
* Parâmetros:
*
*
* Retorno...:
*
*
*
*/

Static Function GridPaginada()


    local lcTable         := 'saldocontratos' as String

    local laRESULT        := {},;
          laPAGINAS       := {},;
          laLIMIT         := {}  as Array

    local lnCOUNT         := 0 as Int

    local laFIELDS        := {'saldocontratos.TP_PASTA',;
                              'saldocontratos.ID_PLATAFORMA',;
                              'plataforma.DESC_PLATAFORMA',;
                              'saldocontratos.CD_CCUSTO',;
                              'centrocusto.NM_CCUSTO',;
                              'saldocontratos.SALDO',;
                              'saldocontratos.CD_JURCC',;
                              'jcentrocusto.NM_CCUSTO as NM_JURCC'} as Array
    local laORDER         := {'saldocontratos.TP_PASTA','plataforma.DESC_PLATAFORMA'}

    local laJoin          := {{1,'plataforma','plataforma.ID_PLATAFORMA = saldocontratos.ID_PLATAFORMA'},;
                              {2,'centrocusto','centrocusto.CD_CCUSTO = saldocontratos.CD_CCUSTO'},;
                              {2,'centrocusto as jcentrocusto','jcentrocusto.CD_CCUSTO = saldocontratos.CD_JURCC'}} as Array

    local lcCODE          := '' as String

    local xx              := 0,;
          ii              := 0 as Int


    local pcFILTRO        := '' as String
    local lcWHERE         := '' as String
/*

WOUT('<b><br> Action:'+Wget('ACTION'))
for ii := 1 to len(laFIELDS)
    wout('<br><b>'+laFIELDS[ii] + ':')
    wout(transform(WGet(laFIELDS[ii])))
next ii
*/
    if Upper(Wget('ACTION')) == 'SEARCH'
       if ! Empty(WGet('TP_PASTA'))
          pcFILTRO := 'saldocontratos.TP_PASTA = ' + DLAP + WGet('TP_PASTA') + DLAP
       endif

       if WGet('DESC_PLATAFORMA','N') > 0
          pcFILTRO += iif(! Empty(pcFILTRO),' and ', '')
          pcFILTRO += 'saldocontratos.ID_PLATAFORMA = ' + WGet('DESC_PLATAFORMA','C')
       endif

       if ! Empty(WGet('CD_CCUSTO'))
          pcFILTRO += iif(! Empty(pcFILTRO),' and ', '')
          pcFILTRO += 'saldocontratos.CD_CCUSTO= ' + DLAP + WGet('CD_CCUSTO') + DLAP
       endif

    endif


   // Rotina para executar paginação

   if db_select({'count(*)'},lcTable) == -1
      db_error()
   endif
   laRESULT  := db_fetchall()
   lnCOUNT   := laRESULT[2,1]
   if lnCOUNT > 0
      laPAGINAS := paginacao(lnCOUNT,'','','mnt.saldoscontratos.html')
      laLIMIT   := param_fetch(laPAGINAS,'LIMIT')
      if empty(pcFILTRO)
          if db_select(laFIELDS,lcTABLE,laJOIN,,laORDER,,,laLIMIT) == -1
            error_sys(db_error())
         endif
      else
          if db_select(laFIELDS,lcTABLE,laJOIN,{pcFILTRO},laORDER,,,laLIMIT) == -1
             error_sys(db_error())
          endif
      endif
      laRESULT := db_fetchall()
      //Resumo da pesquisa
       lcCODE := alltrim(str(laLIMIT[1]+1))+'-'
       if ((len(laRESULT)-1) < (laLIMIT[2]-laLIMIT[1]))
          lcCODE += alltrim(str(len(laRESULT)-1))
       elseif param_fetch(laPAGINAS,'GOTO_PAGE') == param_fetch(laPAGINAS,'NR_PAGES') //lnNR_PAGES
          lcCODE += alltrim(str(lnCOUNT))
       else
          lcCODE += alltrim(str(laLIMIT[1]+param_fetch(laPAGINAS,'ROWS_PAGE')))
       endif
       lcCODE += ' de '+alltrim(str(lnCOUNT))
       if lnCOUNT > 1
          lcCODE := ' Resultados '+lcCODE+' registros'
       else
          lcCODE := ' Resultado '+lcCODE+' registro'
       endif
       wput('RESUME_RESULTSET',lcCODE)

      if len(laRESULT) > 0
         aadd(laRESULT[1],'(row)')
         for ii := 2 to len(laRESULT)
             aadd(laRESULT[ii],'(row)'+wic_action('wicaction/mnt.saldoscontratos.html?ACTION=ALTER'+;
                                                  '&TP_PASTA='+laRESULT[ii,ascan(laRESULT[1],'TP_PASTA')] +;
                                                  '&ID_PLATAFORMA='+Alltrim(laRESULT[ii,ascan(laRESULT[1],'ID_PLATAFORMA')]) +;
                                                  '&CD_CCUSTO='+laRESULT[ii,ascan(laRESULT[1],'CD_CCUSTO')] +;
                                                  '&NM_CCUSTO='+laRESULT[ii,ascan(laRESULT[1],'NM_CCUSTO')] +;
                                                  '&SALDO='+wpictrans('SALDO',laRESULT[ii,ascan(laRESULT[1],'SALDO')]) +;
                                                  '&CD_JURCC='+laRESULT[ii,ascan(laRESULT[1],'CD_JURCC')] +;
                                                  '&NM_JURCC='+laRESULT[ii,ascan(laRESULT[1],'NM_JURCC')] ))

             laRESULT[ii,ascan(laRESULT[1],'SALDO')] := wpictrans('SALDO',laRESULT[ii,ascan(laRESULT[1],'SALDO')])
         next ii

         WPut('GRID_SALDOS',grid_create(laRESULT))
      endif
   endif
   


return(nil)













